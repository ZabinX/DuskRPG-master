if ispet trigger
    endscript
end
livingthing keeper local trigger "Strabo's Keeper"
if ismob keeper
    if < trigger privs 1
        chat trigger "Strabo's Keeper says: None may pass until a leader shows me the key to this castle."
        endscript
    end
    if hasitem trigger "strabo key"
        if or strequals clan trigger global baronclan
                strequals clan trigger global kilgoreclan
            chat trigger "You clan can only control one castle at a time."
            chat trigger "You relinquish control of Castle Strabo."
            removeitem trigger "strabo key"
            set trigger locxy 100 100
            global string straboclan "nostraboclan"
            order trigger concat concat "gossip The " clan trigger " clan no longer controls Castle Strabo."
            createitem "strabo key" 175 161
            endscript
        end
        if < trigger cash 8000
            chat trigger "Strabo's Keeper says: I require 8,000gp to let you pass for a time."
            endscript
        end
        inc trigger cash -8000
        chat trigger "Strabo's Keeper says: You and your clan may pass for a time, but I will be back."
        chat trigger "Strabo's Keeper says: I have left you a little something in the castle treasure room."
        number hour * 60 60
        number day * hour 24
        addconditionwithduration keeper "hidekeeper" * day 4
        global string straboclan clan trigger
        order trigger concat concat "gossip The " global straboclan " clan now controls Castle Strabo."
        createitem "Fire Dragon Scale" 191 153
        createitem "Fire Dragon Tooth" 192 153
        createitem "Fire Dragon Blood" 193 153
        createitem "Ice Dragon Scale" 191 154
        createitem "Ice Dragon Tooth" 192 154
        createitem "Ice Dragon Blood" 193 154
        endscript
    end
    chat trigger "Strabo's Keeper says: Your clan may not pass until you show me the key to this castle and pay my fee."
    endscript
end
if or hascondition trigger mob_attack
        hascondition trigger mob2_attack
    removecondition trigger mob_attack
    removecondition trigger mob2_attack
    chat trigger "You are lucky to have gotten away from the guards alive."
    endscript
end
endscript

